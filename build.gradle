group 'io.falcon'

buildscript {
  repositories {
    mavenCentral()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.9.RELEASE")
    classpath('gradle.plugin.com.palantir.gradle.docker:gradle-docker:0.13.0')
  }
}

group = 'falconiotest'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'com.palantir.docker'

jar {
  baseName = 'io.falcon.falcontest'
  version = '1.0.0-SNAPSHOT'
}

sourceCompatibility = 1.8

repositories {
  mavenCentral()
}

sourceSets {
  generated {
    java {
      srcDirs = ['src/main/generated']
    }
  }
}

configurations {
  querydslapt
}

dependencies {
  compile     group: 'mysql',                    name: 'mysql-connector-java'
  compile     group: 'org.flywaydb',             name: 'flyway-core',                   version:'5.0.6'
  compile     group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
  compile     group: 'org.springframework.boot', name: 'spring-boot-starter-data-redis'
  compile     group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  compile     group: 'org.springframework.boot', name: 'spring-boot-starter-websocket'

  compile     group: 'org.webjars',              name: 'webjars-locator'
  compile     group: 'org.webjars',              name: 'sockjs-client',                 version: '1.0.2'
  compile     group: 'org.webjars',              name: 'stomp-websocket',               version: '2.3.3'
  compile     group: 'org.webjars',              name: 'bootstrap',                     version: '3.3.7'
  compile     group: 'org.webjars',              name: 'jquery',                        version: '3.1.0'

  compile     group: 'com.querydsl',             name: 'querydsl-jpa',                  version: '4.1.4'
  querydslapt group: 'com.querydsl',             name: 'querydsl-apt',                  version: '4.1.4'

  testCompile group: 'com.h2database',           name: 'h2',                            version: '1.4.190'
  testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
}

docker {
  name "${project.group}/${jar.baseName}"
  files jar.archivePath
  buildArgs(['JAR_FILE': "${jar.archiveName}"])
}

task generateQueryDSL(type: JavaCompile, group: 'build', description: 'Generates the QueryDSL query types') {
  source = sourceSets.main.java
  classpath = configurations.compile + configurations.querydslapt
  options.compilerArgs = [
    "-proc:only",
    "-processor", "com.querydsl.apt.jpa.JPAAnnotationProcessor"
  ]
  destinationDir = sourceSets.generated.java.srcDirs.iterator().next()
}

idea {
  module {
    sourceDirs += file('src/main/generated')
  }
}

compileJava {
  dependsOn generateQueryDSL
  source generateQueryDSL.destinationDir
}

compileGeneratedJava {
  dependsOn generateQueryDSL
  options.warnings = false
  classpath += sourceSets.main.runtimeClasspath
}

clean {
  delete sourceSets.generated.java.srcDirs
}